buildscript {
	ext.kotlin_version = '1.3.31'

	repositories {
		jcenter()
		maven { url 'https://maven.google.com' }
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:4.1.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC9.2'
}

detekt {
	toolVersion = '1.0.0.RC9.2'
	input = files("$projectDir")
	config = files("$rootDir/detekt.yml")
	filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
	reports {
		xml {
			enabled = true
			destination = file("$projectDir/reports/detekt.xml")
		}
		html {
			enabled = true
			destination = file("$projectDir/reports/detekt.html")
		}
	}
	idea {
		path = '/Applications/Android Studio.app/Contents'
		codeStyleScheme = "$rootDir/ScreensCodeConvention.xml"
		inspectionsProfile = "$rootDir/android-studio-inspections.xml"
		report = './reports'
		mask = '*.kt,*.java,*.xml,*.css,*.js'
	}
}

ext {
	minSdkVersion = 21
	compileSdkVersion = 30
	targetSdkVersion = compileSdkVersion
	buildToolsVersion = '30.0.2'

	versionCode = 504
	versionName = '5.0.4'

	publishedGroupId = 'com.liferay.mobile'
	libraryName = 'Liferay Screens for Android'
	libraryDescription = 'Liferay Screens for Android includes the component (screenlets) library and a sample project'

	bintrayRepo = 'liferay-mobile'
	bintrayOrg = 'liferay'

	siteUrl = 'https://github.com/liferay/liferay-screens'
	gitUrl = 'https://github.com/liferay/liferay-screens.git'

	developerId = 'nhpatt'
	developerName = 'Javier Gamarra'
	developerEmail = 'nhpatt@gmail.com'

	licenseName = 'GNU Lesser General Public License, version 2.1'
	licenseUrl = 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
	allLicenses = ["LGPL-2.1"]
}

ext.libraries = [junit              : 'junit:junit:4.12',
                 gms                : 'com.google.android.gms:play-services-gcm:9.0.2',
                 sdk                : 'com.liferay.mobile:liferay-android-sdk-core:7.2.0',
                 push               : 'com.liferay.mobile:liferay-push:1.2.1',
                 eventbus           : 'de.greenrobot:eventbus:2.4.0',
                 picasso            : 'com.squareup.picasso:picasso:2.5.2',
                 rxjava             : 'io.reactivex:rxjava:1.2.4',
                 rxpermissions      : 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4',
                 rxbinding          : 'com.jakewharton.rxbinding:rxbinding:1.0.1',
                 robolectric        : 'org.robolectric:robolectric:3.5.1',
                 mockito            : 'org.mockito:mockito-core:2.11.0',
                 snappydb           : 'com.snappydb:snappydb-lib:0.5.2',
                 kryo               : 'com.esotericsoftware:kryo:4.0.0',
                 json               : 'org.json:json:20180813',
                 kotlin             : 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + ext.kotlin_version,
                 espresso           : 'com.android.support.test.espresso:espresso-core:3.0.1']

subprojects {
	repositories {
		mavenLocal()
		maven { url 'https://maven.google.com' }
		jcenter()
	}

	tasks.withType(Javadoc).all {
		failOnError = false
	}
}

repositories {
	mavenLocal()
	maven { url 'https://maven.google.com' }
	jcenter()
	google()
}
