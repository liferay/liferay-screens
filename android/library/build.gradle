buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
		classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}

subprojects {

	version = '1.3.4'
	group = 'com.liferay.mobile'

	repositories {
		jcenter()
	}

	buildscript {
		repositories {
			jcenter()
		}
		dependencies {
			classpath 'com.android.tools.build:gradle:2.1.0'
			classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
			classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
			classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
		}
	}

	apply plugin: 'com.android.library'
	apply plugin: 'maven'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'com.github.dcendents.android-maven'
	apply plugin: 'com.neenbedankt.android-apt'

	def siteUrl = 'https://github.com/liferay/liferay-screens'
	def gitUrl = 'https://github.com/liferay/liferay-screens.git'

	task sourcesJar(type: Jar) {
		from android.sourceSets.main.java.srcDirs
		classifier = 'sources'
	}

	task clean(type: Delete) {
		delete rootProject.buildDir
	}

	task javadoc(type: Javadoc) {
		source = android.sourceSets.main.java.srcDirs
//  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives javadocJar
		archives sourcesJar
	}

	install {
		repositories.mavenInstaller {
			pom {
				project {
					packaging 'aar'

					name 'Liferay Screens for Android includes the component (screenlets) library and a sample project.'
					url siteUrl

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
					developers {
						developer {
							id '1'
							name 'Javier Gamarra'
							email 'nhpatt@gmail.com'
						}
					}
					scm {
						connection gitUrl
						developerConnection gitUrl
						url siteUrl

					}
				}
			}
		}
	}

	Properties properties = new Properties()
	properties.load(project.rootProject.file('local.properties').newDataInputStream())

	bintray {
		user = properties.getProperty('bintray.user')
		key = properties.getProperty('bintray.apikey')

		configurations = ['archives']
		pkg {
//     		userOrg = 'liferay'
			repo = 'liferay-mobile'
			name = project.archivesBaseName
			websiteUrl = siteUrl
			vcsUrl = gitUrl
			licenses = ['LGPL-2.1']
			publish = true
		}
	}
}