apply plugin: 'com.android.library'

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.dcendents:android-maven-plugin:1.2'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
	}
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 22
	buildToolsVersion "22.0.1"

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 22
		versionCode 1
		versionName "1.0"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

}

def siteUrl = 'https://github.com/liferay/liferay-screens'
def gitUrl = 'https://github.com/liferay/liferay-screens.git'
version = '0.4.1'
group = 'com.liferay.mobile'

project.archivesBaseName = 'liferay-screens'

install {
	repositories.mavenInstaller {
		// This generates POM.xml with proper parameters
		pom {
			project {
				packaging 'aar'

				// Add your description here
				name 'Liferay Screens for Android includes the component (screenlets) library and a sample project.'
				url siteUrl

				// Set your license
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id '1'
						name 'Javier Gamarra'
						email 'nhpatt@gmail.com'
					}
				}
				scm {
					connection gitUrl
					developerConnection gitUrl
					url siteUrl

				}
			}
		}
	}
}

repositories {
	mavenLocal()

	maven {
		url 'https://repository.liferay.com/nexus/content/groups/liferay-ce'
	}
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

	jcenter()
	mavenCentral()
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.android.support:appcompat-v7:22.1.1'
	compile 'com.android.support:recyclerview-v7:22.1.1'
	compile 'com.liferay.mobile:liferay-android-sdk:6.2.0.13'
	compile 'de.greenrobot:eventbus:2.4.0'
	compile 'com.squareup.picasso:picasso:2.5.0'
	compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
		exclude group: 'com.google.android', module: 'support-v4'
	}

	testCompile "junit:junit:4.12"
	testCompile "org.robolectric:robolectric:3.0-rc2"
	testCompile 'org.mockito:mockito-core:1.9.5'

}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
//	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}
artifacts {
	archives javadocJar
	archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")

	configurations = ['archives']
	pkg {
		repo = "maven"
		name = "liferay-screens"
		websiteUrl = siteUrl
		vcsUrl = gitUrl
		licenses = ["LGPL-2.1"]
		publish = true
	}
}